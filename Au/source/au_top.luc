/**********************************************************************************
   MicroFPGA configuration for the Au board. It controls by default the triggering 
   of 8 lasers (from a camera signal), 5 PWM signals, 7 servos, 4 TTL outputs and 8
   analog input channels.
         
   Visit https://mufpga.github.io/ for the documentation and guide to using MicroFPGA,
   in Micro-Manager, Python, Java and LabView, as well as details of complementary
   electronics. 
      
   Written by Joran Deschamps, EMBL (2019)
   https://mufpga.github.io/ 
   GPL-3.0 License  
*/

module au_top (
    input clk,              // 100MHz clock
    input rst_n,            // reset button (active low)
    output led [8],         // 8 user controllable LEDs
    input usb_rx,           // USB->Serial input
    output usb_tx,           // USB->Serial output
    input camera_in,
    output camera_out,
    output laser0,
    output laser1,
    output laser2,
    output laser3,
    output laser4,
    output laser5,
    output laser6,
    output laser7,
    output ttl0,
    output ttl1,
    output ttl2,
    output ttl3,
    output servo0,
    output servo1,
    output servo2,
    output servo3,
    output servo4,
    output servo5,
    output servo6,
    output pwm0,
    output pwm1,
    output pwm2,
    output pwm3,
    output pwm4,
    input vp0,
    input vn0,
    input vp1,
    input vn1,
    input vp2,
    input vn2,
    input vp3,
    input vn3,
    input vp4,
    input vn4,
    input vp5,
    input vn5,
    input vp6,
    input vn6,
    input vp7,
    input vn7,
    input vp,
    input vn
  ) {
     
  // number of signals
  const NUM_LASERS = 8;
  const NUM_TTL = 4;
  const NUM_PWM = 5;
  const NUM_SERVOS = 7;
  const NUM_ANALOG = 8; // fixed
    
  // base addresses for each signal type
  const ADDR_MODE = 0; 
  const ADDR_DUR = NUM_LASERS; // 8
  const ADDR_SEQ = ADDR_DUR+NUM_LASERS; // 16
  const ADDR_TTL = ADDR_SEQ+NUM_LASERS; // 24
  const ADDR_SERVOS = ADDR_TTL+NUM_TTL; // 29
  const ADDR_PWM = ADDR_SERVOS+NUM_SERVOS; // 36
  
  const ADDR_ACTIVE_TRIGGER = ADDR_PWM+NUM_PWM; // 41
  const ADDR_START_TRIGGER = ADDR_ACTIVE_TRIGGER+1; // 42
  const ADDR_CAM_PULSE = ADDR_START_TRIGGER+1; // 43
  const ADDR_CAM_PERIOD = ADDR_CAM_PULSE+1; // 44
  const ADDR_CAM_EXPO = ADDR_CAM_PERIOD+1; // 45
  const ADDR_LASER_DELAY = ADDR_CAM_EXPO+1; // 46
  
  const ADDR_AI = ADDR_LASER_DELAY+1;// 47

  const ADDR_VERSION = 200;
  const ADDR_ID = 201;
  
  // constants returned
  const VERSION = 3;  
  const ID = 79; // Au atomic number
  const ERROR_UNKNOW_COMMAND = 11206655; // only answer with meaningful data in the 3rd byte
    
  sig rst;  // reset signal
   
  .clk(clk) {
    reset_conditioner reset_cond;
     
    .rst(rst){
      
      uart_rx rx (#BAUD(57600), #CLK_FREQ(100000000)); // serial receiver
      uart_tx tx (#BAUD(57600), #CLK_FREQ(100000000)); // serial transmitter
      
      reg_interface reg;
          
      // lasers
      laser_trigger l[NUM_LASERS];
      signal_sync framesync; // to synchronize all lasers on the same frame counter
      
      dff sequence[NUM_LASERS][16];
      dff duration[NUM_LASERS][16];
      dff mode[NUM_LASERS][3];
      
      // main trigger
      camera_trigger trigger;
      dff active_trigger;
      dff start_trigger;
      dff cam_pulse[16]; // pulse length of the camera trigger signal
      dff cam_period[16]; // period of the camera trigger signal
      dff cam_exposure[16]; // camera exposure
      dff laser_delay[16]; // laser trigger delay
      
      // ttls
      dff ttl[NUM_TTL];
      
      // servos
      servo_standard servo_controller[NUM_SERVOS];
      dff position[NUM_SERVOS][16];
      dff servo_sig_update[NUM_SERVOS];
      servo_stop servo_sig[NUM_SERVOS]; // to shut down the servos 10 sec after every movement
      
      // pwm
      pwm pulsewm[NUM_PWM](#TOP(254),#DIV(9),#WIDTH(8));
      dff dutycycle[NUM_PWM][8];
      dff pwmupdate[NUM_PWM];
      
      // analog reader
      analog analog_read;
      dff analog_out[NUM_ANALOG][16];
    }
  }

  always {
    led = 8b0;  
    tx.block = 0; // no blocking
  
    reset_cond.in = ~rst_n; // input raw inverted reset signal
    rst = reset_cond.out;   // conditioned reset
    
    rx.rx = usb_rx;         // connect rx input
    usb_tx = tx.tx;         // connect tx output 
         
    // connect reg interface to avr interface
    reg.rx_data = rx.data;
    reg.new_rx_data = rx.new_data;
    tx.data = reg.tx_data;
    tx.new_data = reg.new_tx_data;
    reg.tx_busy = tx.busy;
     
    reg.regIn.drdy = 0;                   // default to not ready
    reg.regIn.data = 32bx;               
        
    // updates default to 0
    pwmupdate.d = NUM_PWMx{0};
    servo_sig_update.d = NUM_SERVOSx{0};
    
    /////////////////////////////////////////////////////////
    /// Communication based on the register interface
    /// (see Alchitry Lucid tutorials) 
    if (reg.regOut.new_cmd) { // new command
      if (reg.regOut.write) { // if write
        if (reg.regOut.address < ADDR_MODE+NUM_LASERS) {         // Laser modes  
          mode.d[reg.regOut.address-ADDR_MODE] = reg.regOut.data[2:0];     
        } else if (reg.regOut.address < ADDR_DUR+NUM_LASERS) {         // Laser duration
          duration.d[reg.regOut.address-ADDR_DUR] = reg.regOut.data[15:0]; 
        } else if (reg.regOut.address < ADDR_SEQ+NUM_LASERS) {            // Laser sequence
          sequence.d[reg.regOut.address-ADDR_SEQ] = reg.regOut.data[15:0];    
        } else if (reg.regOut.address < ADDR_TTL+NUM_TTL){           // TTL
          ttl.d[reg.regOut.address-ADDR_TTL] = reg.regOut.data[0];
        } else if (reg.regOut.address < ADDR_SERVOS+NUM_SERVOS){           // Servo
          position.d[reg.regOut.address-ADDR_SERVOS] = reg.regOut.data[15:0];
          servo_sig_update.d[reg.regOut.address-ADDR_SERVOS] = 1;
        } else if (reg.regOut.address < ADDR_PWM+NUM_PWM){           // PWM
          dutycycle.d[reg.regOut.address-ADDR_PWM] = reg.regOut.data[7:0];
          pwmupdate.d[reg.regOut.address-ADDR_PWM] = 1;
        } else if (reg.regOut.address == ADDR_ACTIVE_TRIGGER){      // Active/passive camera trigger
          active_trigger.d = reg.regOut.data[0];
        } else if (reg.regOut.address == ADDR_START_TRIGGER){      // Camera trigger start
          start_trigger.d = reg.regOut.data[0];
        } else if (reg.regOut.address == ADDR_CAM_PULSE){      // Camera trigger length
          cam_pulse.d = reg.regOut.data[15:0];
        } else if (reg.regOut.address == ADDR_CAM_PERIOD){      // Camera trigger period
          cam_period.d = reg.regOut.data[15:0];
        } else if (reg.regOut.address == ADDR_CAM_EXPO){      // Exposure length
          cam_exposure.d = reg.regOut.data[15:0];
        } else if (reg.regOut.address == ADDR_LASER_DELAY){      // Laser trigger delay
          laser_delay.d = reg.regOut.data[15:0];
        } 
      } else { // read
         if (reg.regOut.address < ADDR_MODE+NUM_LASERS) {                // Laser modes 
          reg.regIn.data = mode.q[reg.regOut.address-ADDR_MODE];        
          reg.regIn.drdy = 1;    
        } else if (reg.regOut.address < ADDR_DUR+NUM_LASERS) {       // Laser duration
          reg.regIn.data = duration.q[reg.regOut.address-ADDR_DUR];        
          reg.regIn.drdy = 1;    
        } else if (reg.regOut.address < ADDR_SEQ+NUM_LASERS) {       // Laser sequence
          reg.regIn.data = sequence.q[reg.regOut.address-ADDR_SEQ];        
          reg.regIn.drdy = 1;       
        } else if (reg.regOut.address < ADDR_TTL+NUM_TTL){           // TTL
          reg.regIn.data = ttl.q[reg.regOut.address-ADDR_TTL];        
          reg.regIn.drdy = 1;
        } else if (reg.regOut.address < ADDR_SERVOS+NUM_SERVOS){        // Servo
          reg.regIn.data = position.q[reg.regOut.address-ADDR_SERVOS];        
          reg.regIn.drdy = 1;
        } else if (reg.regOut.address < ADDR_PWM+NUM_PWM){           // PWM
          reg.regIn.data = dutycycle.q[reg.regOut.address-ADDR_PWM];        
          reg.regIn.drdy = 1;     
        } else if (reg.regOut.address == ADDR_ACTIVE_TRIGGER) {    // Active/passive camera trigger
          reg.regIn.data = active_trigger.q;      
          reg.regIn.drdy = 1;             
        } else if (reg.regOut.address == ADDR_START_TRIGGER) {    // Camera trigger start
          reg.regIn.data = start_trigger.q;      
          reg.regIn.drdy = 1;             
        } else if (reg.regOut.address == ADDR_CAM_PULSE) {    // Camera trigger length   
          reg.regIn.data = cam_pulse.q;     
          reg.regIn.drdy = 1;             
        } else if (reg.regOut.address == ADDR_CAM_PERIOD) {    // Camera trigger period   
          reg.regIn.data = cam_period.q;     
          reg.regIn.drdy = 1;             
        } else if (reg.regOut.address == ADDR_CAM_EXPO) {    // Camera exposure length  
          reg.regIn.data = cam_exposure.q;     
          reg.regIn.drdy = 1;             
        } else if (reg.regOut.address == ADDR_LASER_DELAY) {    // Laser trigger delay  
          reg.regIn.data = laser_delay.q;     
          reg.regIn.drdy = 1;             
        } else if (reg.regOut.address < ADDR_AI+NUM_ANALOG) {        // Analog input   
          reg.regIn.data = analog_out.q[reg.regOut.address-ADDR_AI];        
          reg.regIn.drdy = 1;             
        } else if (reg.regOut.address == ADDR_VERSION) {    // Version    
          reg.regIn.data = VERSION; // version number      
          reg.regIn.drdy = 1;             
        } else if (reg.regOut.address == ADDR_ID) {    // ID   
          reg.regIn.data = ID; // id    
          reg.regIn.drdy = 1;             
        } else { // Error
          reg.regIn.data = ERROR_UNKNOW_COMMAND;        
          reg.regIn.drdy = 1; 
        }    
      }
    }
    
    //////////////// TTLs
    ttl0 = ttl.q[0];
    ttl1 = ttl.q[1];
    ttl2 = ttl.q[2];
    ttl3 = ttl.q[3];
    
    //////////////// main trigger (triggering camera and lasers)
    
    // report trigger mode on LED
    led[7] = active_trigger.q;    
    
    if(active_trigger.q){ // active trigger: the FPGA triggers both camera and lasers
      // parameters from serial communication
      trigger.start = start_trigger.q;
      trigger.trigger_pulse = cam_pulse.q;
      trigger.trigger_period = cam_period.q;
      trigger.trigger_exposure = cam_exposure.q;
      trigger.trigger_delay = laser_delay.q;
      
      // feed the exposure signal to the lasers
      framesync.camera = trigger.trigger_lasers; // inter-laser sync module
      l.cam_sig = NUM_LASERSx{trigger.trigger_lasers};
      
      // trigger camera
      camera_out = trigger.trigger_camera;
      
      led[0] = start_trigger.q;
      led[1] = trigger.trigger_camera;
      led[2] = trigger.trigger_lasers;
      led[3] = l.lasersignal[0];
      led[4] = l.lasersignal[1];
      led[5] = l.lasersignal[2];
      led[6] = l.lasersignal[3]; 
      
    } else { // passive trigger: external camera triggers the FPGA/lasers
    
      trigger.start = 0;
      trigger.trigger_pulse = 16x{0};
      trigger.trigger_period = 16x{0};
      trigger.trigger_exposure = 16x{0};
      trigger.trigger_delay = 16x{0};
      
      camera_out = 0;
      
      framesync.camera = camera_in;
      l.cam_sig = NUM_LASERSx{camera_in};
      
      led[2:0] = 3x{1};
    }
    
    ///////////////// Lasers	 
    l.seq = sequence.q;
    l.mod = mode.q;
    l.dura = duration.q; 
    l.sync = NUM_LASERSx{{framesync.sync}};
    
    laser0 = l.lasersignal[0]; // laser trigger outputs
    laser1 = l.lasersignal[1];
    laser2 = l.lasersignal[2];
    laser3 = l.lasersignal[3];
    laser4 = l.lasersignal[4];
    laser5 = l.lasersignal[5];
    laser6 = l.lasersignal[6];
    laser7 = l.lasersignal[7];
            
    //////////////// Servos
    servo_controller.position = position.q;
    servo_sig.update = servo_sig_update.q;
    servo_sig.signal_in = servo_controller.servo;
    servo0 = servo_sig.signal_out[0]; // servo outputs from the servo_stop module
    servo1 = servo_sig.signal_out[1];
    servo2 = servo_sig.signal_out[2];
    servo3 = servo_sig.signal_out[3];
    servo4 = servo_sig.signal_out[4];
    servo5 = servo_sig.signal_out[5];
    servo6 = servo_sig.signal_out[6];
    
    //////////////// PWM
    pulsewm.update = pwmupdate.q;
    pulsewm.value = dutycycle.q;
    pwm0 = pulsewm.pulse[0];
    pwm1 = pulsewm.pulse[1];
    pwm2 = pulsewm.pulse[2];
    pwm3 = pulsewm.pulse[3];
    pwm4 = pulsewm.pulse[4];
    
    //////////////// Analog read
    analog_read.vp0 = vp0;
    analog_read.vn0 = vn0;
    analog_read.vp1 = vp1;
    analog_read.vn1 = vn1;
    analog_read.vp2 = vp2;
    analog_read.vn2 = vn2;
    analog_read.vp3 = vp3;
    analog_read.vn3 = vn3;
    analog_read.vp4 = vp4;
    analog_read.vn4 = vn4;
    analog_read.vp5 = vp5;
    analog_read.vn5 = vn5;
    analog_read.vp6 = vp6;
    analog_read.vn6 = vn6;
    analog_read.vp7 = vp7;
    analog_read.vn7 = vn7;
    analog_read.vp = vp;
    analog_read.vn = vn;
    analog_out.d = analog_read.signals;
  }
}