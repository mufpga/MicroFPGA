/******************************************************************************
   This module generates two TTL signals intended at triggering a camera and 
   lasers. The fire trigger output can be used directly to trigger a camera,
   while the exposure trigger output can be processed by a laser_trigger module.
   
   It takes several parameters:
   * start: the output signals are only generated when start is HIGH, they are 
           0 otherwise.
   * pulse: pulse length of the fire trigger signal.
   * readout: period between the end of exposure and the next fire pulse.
   * exposure: pulse length of the exposure trigger signal.
   * delay: delay of theexposure trigger signal with respect to the fire trigger.
      
   Outputs:
   * fire_trigger: camera fire signal.
   * exposure_trigger: camera exposure signal, intended to trigger laser_trigger
                     modules.
   
   Example: 
                                           readout
                                             <->
               <-pulse->
               ---------                        ---------      high    
              |         |                      |         |
   camera -----         ------------------------         ----- low
   
                 <---------exposure--------->
                  ---------------------------      ----------- high
                 |                           |    |        
   exposure ------                           ------            low
              <->delay
   
   Written by Joran Deschamps, EMBL (2021)
   https://mufpga.github.io/ 
   GPL-3.0 License  
*/
module camera_trigger (
    input clk,  // clock
    input rst,  // reset
    input start,
    input pulse[20], // pulse length of the camera trigger
    input readout[16], // period between the end of the exposure and the next fire
    input exposure[20], // exposure of a camera frame (for the lasers)
    input delay[16], // delay for the laser exposure
    output fire_trigger,
    output exposure_trigger
  ) {

  // 16 bits -> maximum of 65.535 ms with US_CYCLES = 100, since clock cycle = 100MHz
  // 20 bits -> maximum of 1.048575 s
  const US_CYCLES = 100; // US_CYCLES x 1e-8 = ~1 us
  var pulse_cycle;
  var period_cycle;
  var exposure_cycle;
  var delay_cycle;
  
  .clk(clk){ 
    .rst(rst) {
      // If x is the number of bits of pulse/delay/exposure/readout
      // i.e. Nbits(trigger_length) = X
      // Then, we must have Nbits(counter) > log2((2^X-1)*CYCLES+1)
      // x=20 and US_CYCLES=100 => Nbits(counter) > 26.6
      // x=16 and US_CYCLES=100 => Nbits(counter) > 22.6
      dff counter[27]; // cycles counter
      dff delay_counter[23]; // delay counter
    }
  }
  
  always {
    pulse_cycle = pulse*US_CYCLES;
    period_cycle = (delay+readout+exposure)*US_CYCLES;
    exposure_cycle = exposure*US_CYCLES;
    delay_cycle = delay*US_CYCLES;
    
    // increase counters until they max out
    if (!&counter.q){ 
      counter.d = counter.q + 1;
    }
    if (!&delay_counter.q){ 
      delay_counter.d = delay_counter.q + 1;
    }
    
    // reset counters every period
    if (counter.q >= period_cycle && start){
      counter.d = 0;
      delay_counter.d = 0;
    }
    
    // set output signals
    fire_trigger = counter.q < pulse_cycle;
    exposure_trigger = delay_counter.q > delay_cycle && counter.q < delay_cycle+exposure_cycle;    
  }
}
