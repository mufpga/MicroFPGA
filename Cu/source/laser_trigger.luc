/******************************************************************************
   This module triggers a laser using a TTL signal based the processing of a 
   camera triggering input.
   
   The laser triggering module takes multiple inputs:
   * cam_sig: camera triggering signal (high when exposing, low when registring 
              the pixel values).
   * mode: only values 0 to 4 are considered, the modes are ON, OFF, pusing on 
           camera rising edges (RISING), pulsing on camera falling edges (FALLING)
           and following the camera signal (FOLLOW).
   * duration: when the mode is set to pulsing (RISING or FALLING), the laser is 
               pulsed for <duration> us.  
   * sequence: a 16 bits sequence determining the triggering pattern of the laser.
               On a 0, the laser will remain off during the corresponding frame. 
               On a 1, it will be triggered.
   * sync: sequence counter given by a signal_sync module.
   
   Written by Joran Deschamps, EMBL (2019)
   https://mufpga.github.io/ 
   GPL-3.0 License  
*/

module laser_trigger (
    input clk,  // clock
    input rst,  // reset
    input cam_sig,
    input mod[3],
    input dura[20],
    input seq[16],
    input sync[4],
    output lasersignal
  ) {
 
  const OFF = 0;
  const ON = 1;
  const RISING = 2;
  const FALLING = 3;
  const FOLLOW = 4;
  
  const NM_CYCLES = 100; // convert to ~us since NM_CYCLES x 1e-8 = 1e-6
  var plength;
    
 .clk(clk){ 
    .rst(rst) {
      dff sig_sync[4];
      dff sig_old;
      dff count_sig[27];
  }}
  
  always {
    // If x is the number of bits of dura
    // i.e. Nbits(dura) = X
    // Then, we must have Nbits(counter) > log2((2^X-1)*NM_CYCLES+1)
    // X=20, NM_CYCLES=100 => Nbits(count_sig) > 26.6
    // X=16, NM_CYCLES=100 => Nbits(count_sig) > 22.6
    plength = dura*NM_CYCLES;
    
    sig_sync.d[0] = cam_sig;
    sig_sync.d[1] = sig_sync.q[0];
    sig_sync.d[2] = sig_sync.q[1]; // 2 clock cycles to leave time for sync value to update
    sig_sync.d[3] = sig_sync.q[2];
    sig_old.d = sig_sync.q[3];
    
    if (!&count_sig.q){ 
      count_sig.d = count_sig.q + 1;
    }
    
    lasersignal=0;
  
    case(mod){
      OFF:
        lasersignal = 0;
      ON:
        lasersignal = 1;
      RISING:
        if(sig_old.q == 0 &&  sig_sync.q[3] == 1){ // detect rising edge
            count_sig.d = 0;
        }
        lasersignal = (sig_sync.q[1] && seq[15-sync]) && count_sig.q<plength;
      FALLING:
        if(sig_old.q == 1 &&  sig_sync.q[3] == 0){ // detect falling edge
            count_sig.d = 0;
        }
        lasersignal = (!sig_sync.q[3] && seq[15-sync]) && count_sig.q<plength;
      FOLLOW:
        lasersignal = (sig_sync.q[3] && seq[15-sync]);
    }
  }
}
